// script._OptParams ( _script )
// Purpose:		Returns a list of optional script parameters according to the script name and the
//				FileMakerStandards.org convention for listing parameters in script names
// Parameters:		_script:	The name of a script, optional, `Get ( ScriptName )` used when blank
// Version:		1.0 written 12-12-07 by Jeremy Bante
//				1.1 edited 13-06-29 by Jeremy Bante to append a trailing return to non-empty results
//					to support direct concatenation with other lists
// Notes:			http://filemakerstandards.org/display/cs/Script+naming
//				Scripts should be named as follows:
//				"Script Name ( Req1 ; Req2 {; Opt1 ; Opt2 } )"
// Example:		sample = result

Case (
  // ========================================
  // Step 0, set-up
  not $scpm.OptionalParams.step ;
  Let (
    [
      _params = script._RawParams ( _script ; "optional" ) ;

      $scpm.OptionalParams.count = ValueCount ( _params ) ;

      $scpm.OptionalParams.step = Case (
        IsEmpty ( _params )	; 2
					; 1
      )
    ] ;

    script._OptParams ( _params )
  ) ;

  // ========================================
  // Step 1, trim whitespace
  $scpm.OptionalParams.step = 1 ;
  Let (
    [
      $scpm.OptionalParams.i = $scpm.OptionalParams.i + 1 ;
      _param = Trim ( GetValue ( _script ; $scpm.OptionalParams.i ) ) ;
      $scpm.OptionalParams.result = List ( $scpm.OptionalParams.result ; _param ) ;

      $scpm.OptionalParams.step = Case (
        $scpm.OptionalParams.i < $scpm.OptionalParams.count	; 1
												; 2
      )
    ] ;

    script._OptParams ( _script )
  ) ;

  // ========================================
  // Step 2, clean-up and return result
  $scpm.OptionalParams.step = 2 ;
  Let (
    [
      _result = Case (
        not IsEmpty ( $scpm.OptionalParams.result ) ;
        $scpm.OptionalParams.result & Â¶
      ) ;

      // purge variables
      $scpm.OptionalParams.i		= dev.Nil ;
      $scpm.OptionalParams.count	= dev.Nil ;
      $scpm.OptionalParams.result	= dev.Nil ;
      $scpm.OptionalParams.step	= dev.Nil
    ] ;

    _result
  )
)
