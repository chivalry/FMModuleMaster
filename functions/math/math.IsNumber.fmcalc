// math.IsNumber ( _value )
// Purpose:		Determine if the supplied value is numeric
// Parameters:		_value: The value to test.
// Version:		1.0 - Unknown - 15-03-11
// Notes:			Adapted from http://fmfunctions.com/functions_display_record.php?functionId=161
// Example:		math.IsNumber ( 0 ) and math.IsNumber ( -0 ) and math.IsNumber ( 0.1 ) and math.IsNumber ( 1 ) and math.IsNumber ( 1.2 )
//				  and math.IsNumber ( -1.2 ) and math.IsNumber ( 1.234e+124 ) and math.IsNumber ( -1.234e+124 )
//				  and math.IsNumber ( 10^50 ) and ( not math.IsNumber ( "abc" ) ) and ( not math.IsNumber ( "1.2.3" ) )
//				  and ( not math.IsNumber ( "0001" ) )

Let(
  [
    _decimal_separator = Middle( 3 / 2; 2; 1 ); // do some division to get separator

    _cleaned_value = Substitute(
      _value ;
      [ "e+" ;	"" ];
      [ "E+" ;	"" ];
      [ "e-" ;		"" ];
      [ "E-" ;		"" ]
    );

    _filtered_value = Filter( _cleaned_value; "1234567890-" & _decimal_separator );

    _decimal_count = PatternCount( _value; _decimal_separator )
  ];

  Exact( _filtered_value ; _cleaned_value )
    and ( Left( _value; 1 ) ≠ "0"
      or _value = "0"
      or Left( _value; 2 ) = "-0"
      or Left( _value; 2 ) = "0" & _decimal_separator
    )
    and _decimal_count ≤ 1
    and _value ≠ "-"
)
