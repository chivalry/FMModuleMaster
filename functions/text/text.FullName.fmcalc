// text.FullName ( _prefix; _first; _middle; _last; _suffix )
// Purpose:		Return a person's full name from the piece fields.
// Parameters:		_prefix:	Mr, Mrs, Dr, etc.
//				_first:	The first name.
//				_middle:	The middle name.
//				_last:		The last name.
//				_suffix:	Ph.D., etc.
// Version:		1.0 - Charles Ross - 15-03-11
// Example:		txtf.FullName ( "Mr."; "Charles"; "Edward"; "Ross"; "Ph.D" ) = "Mr. Charles Edward Ross, Ph.D."

Let (
  [
    // List function will not create blank members if entries are empty string.
    _before_suffix = List (
      _prefix ;
      _first ;
      _middle ;
      _last
    ) ;

    _before_suffix	= list.ReplaceSeparator ( _before_suffix ; " " ) ;
    _suffix			= Case ( dev.IsSet ( _suffix ) ; ", " & _suffix ) ;
    _result			= _before_suffix & _suffix
  ];

  _result
)
