// txtf.FormatAddress ( _com; _con; _addr_1; _addr_2; _city; _state; _zip; _country )
// Purpose:		Returns a formatted address given the provided parameters.
// Parameters:		_com:	The company name.
//				_con:	The contact name.
//				_addr_1:	The first address line.
//				_addr_2:	The second address line.
//				_city:		The city.
//				_state:	The state.
//				_zip:		The postal code.
//				_country:	The country.
// Version:		1.0 - Charles Ross - 15-03-11
// Notes:			Slide any content that is missing from the full address. This will display the address
//				formatting without carriage returns for empty content in the fields. It will also remove
//				unwanted leading and trailing spaces with the Trim function. The only thing it won't
//				accomodate is an empty field with a carriage returns.
// Example:
// txtf.FormatAddress ( "Chivalry Software" ; "Charles Ross" ; "123 Main St." ; "Ste. 10" ; "Los Angeles" ; "CA" ; 
//   "90024" ; "USA" ) = "Chivalry Software¶Charles Ross¶123 Main St.¶Ste. 10¶Los Angeles, CA 90024¶USA"

Case( IsEmpty( _com );		dev.Nil ;		   Trim( _com ) ) &
Case( IsEmpty( _con );		dev.Nil ;	"¶"	& Trim( _con ) ) &
Case( IsEmpty( _addr_1 );		dev.Nil ;	"¶"	& Trim( _addr_1 ) ) &
Case( IsEmpty( _addr_2 );		dev.Nil ;	"¶"	& Trim( _addr_2 ) ) &
Case( IsEmpty( _city );		dev.Nil ;	"¶"	& Trim( _city ) ) &
Case( IsEmpty( _state );		dev.Nil ;	", "	& Trim( _state ) ) &
Case( IsEmpty( _zip );		dev.Nil ;	" "	& Trim( _zip ) ) &
Case( IsEmpty( _country );		dev.Nil ;	"¶"	& Trim( _country ) )
